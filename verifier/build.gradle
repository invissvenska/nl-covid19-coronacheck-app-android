plugins {
    id "com.android.application"
    id "kotlin-android"
    id "kotlin-kapt"
    id 'kotlin-parcelize'
    id "androidx.navigation.safeargs.kotlin"
}

def appVersionCode =  1000000
if (System.getenv("GITHUB_REPOSITORY_OWNER") == "minvws") {
    appVersionCode = System.getenv("GITHUB_RUN_NUMBER") != null ? System.getenv("GITHUB_RUN_NUMBER").toInteger() : 1000000
}
version = "3.0.2"
archivesBaseName = "verifier-v${version}-${appVersionCode}"

android {
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    buildFeatures {
        viewBinding true
    }

    defaultConfig {
        applicationId "nl.rijksoverheid.ctr.verifier"
        versionCode appVersionCode
        versionName version
        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "SIGNATURE_CERTIFICATE_CN_MATCH", '".coronatester.nl"'
        buildConfigField "boolean", "FEATURE_CORONA_CHECK_API_CHECKS", "true"
        buildConfigField "boolean", "FEATURE_TEST_PROVIDER_API_CHECKS", "true"
        buildConfigField "String", "BASE_API_URL", "\"https://verifier-api.coronacheck.nl/v8/\""
        buildConfigField "String[]", "CERTIFICATE_PINS",
                "{" +
                        "\"sha256/L9V8t4JfWfEan29aaqxEzpnfBnXhjLIWTX9eu7D4=\"" +
                        "}"
        buildConfigField "String", "CDN_API_URL", "\"https://verifier-api-cdn.coronacheck.nl/v8/\""
        manifestPlaceholders = [appLabel: "@string/app_name", deepLinkHost: "coronacheck.nl"]
        ndk {
            debugSymbolLevel "FULL"
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation":
                                      "$projectDir/schemas".toString()]
            }
        }
    }

    signingConfigs {
        release {
            storeFile file('../keystore.jks')
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("KEYSTORE_KEY_ALIAS")
            keyPassword System.getenv("KEYSTORE_KEY_PASSWORD")
        }
    }

    flavorDimensions "environment"
    productFlavors {
        acc {
            dimension "environment"
            versionNameSuffix "-acc"
            applicationIdSuffix ".acc"
            manifestPlaceholders = [appLabel: "@string/app_name_acc"]
            buildConfigField "String", "BASE_API_URL", "\"https://verifier-api.acc.coronacheck.nl/v8/\""
            buildConfigField "String", "CDN_API_URL", "\"https://verifier-api-cdn.acc.coronacheck.nl/v8/\""
            buildConfigField "String[]", "CERTIFICATE_PINS",
                    "{" +
                            "\"sha256/L9V8t4JfWfEan29aaqxEzpnfBnXhjLIWTX9eu7D4=\"" +
                            "}"
            apply plugin: 'com.google.gms.google-services'
            apply plugin: 'com.google.firebase.crashlytics'
            manifestPlaceholders = [appLabel: "@string/app_name_acc", deepLinkHost: "web.acc.coronacheck.nl"]
            matchingFallbacks = ['mlkit']
        }
        fdroidAcc {
            dimension "environment"
            versionNameSuffix "-fdroid"
            applicationIdSuffix ".fdroid.acc"
            manifestPlaceholders = [appLabel: "@string/app_name_acc"]
            buildConfigField "String", "BASE_API_URL", "\"https://verifier-api.acc.coronacheck.nl/v8/\""
            buildConfigField "String", "CDN_API_URL", "\"https://verifier-api-cdn.acc.coronacheck.nl/v8/\""
            buildConfigField "String[]", "CERTIFICATE_PINS",
                    "{" +
                            "\"sha256/L9V8t4JfWfEan29aaqxEzpnfBnXhjLIWTX9eu7D4=\"" +
                            "}"
            manifestPlaceholders = [appLabel: "@string/app_name_acc", deepLinkHost: "web.acc.coronacheck.nl"]
            matchingFallbacks = ['zxing']
        }
        prod {
            dimension "environment"
            matchingFallbacks = ['mlkit']
        }
        fdroidProd {
            dimension "environment"
            matchingFallbacks = ['zxing']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            productFlavors.acc.signingConfig signingConfigs.release
            productFlavors.prod.signingConfig signingConfigs.release
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
    }
}

dependencies {
    implementation project(":shared")
    implementation project(":appconfig")
    implementation project(":design")
    implementation project(":introduction")
    implementation project(":api")
    implementation project(':mobilecore')
    implementation project(":qrscanner")

    implementation "com.github.lisawray.groupie:groupie:${groupie_version}"
    implementation "com.github.lisawray.groupie:groupie-viewbinding:${groupie_version}"
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"
    implementation "io.insert-koin:koin-android:$koin_version"
    implementation "androidx.preference:preference-ktx:$preference_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayout_version"
    implementation "com.jakewharton.timber:timber:${timber_version}"
    implementation "androidx.security:security-crypto:$security_crypto_version"
    implementation "com.google.mlkit:barcode-scanning:${mlkit_barcode_version}"
    implementation(platform("com.squareup.okhttp3:okhttp-bom:$okhttp_version"))
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    implementation "net.zetetic:android-database-sqlcipher:$sqlcypher_version"
    implementation "com.airbnb.android:lottie:$lottie_version"
    kapt "androidx.room:room-compiler:$room_version"
    testImplementation "androidx.room:room-testing:$room_version"


    testImplementation "androidx.test:runner:$androix_test_version"
    testImplementation "junit:junit:$junit_version"
    testImplementation "io.mockk:mockk:$mock_version"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"
    testImplementation "androidx.arch.core:core-testing:$androidx_arch_core_version"
    testImplementation("org.robolectric:robolectric:$robolectric_version") {
        exclude group: 'org.bouncycastle'
    }
    testImplementation("com.adevinta.android:barista:$barista_version") {
        exclude group: "org.jetbrains.kotlin"
    }
    testImplementation "io.insert-koin:koin-test-junit4:$koin_version"
    testImplementation "androidx.navigation:navigation-testing:$navigation_version"

    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:$desugar_jdk_version"

    accImplementation "com.google.firebase:firebase-crashlytics-ktx:$crashlytics_version"

    debugImplementation "androidx.fragment:fragment-testing:$fragment_version"

    // Needed in order for room to work with Apple Sillicon M1
    kapt "org.xerial:sqlite-jdbc:$sqlite_jdbc"
}
